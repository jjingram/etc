set nocompatible
filetype off
set rtp+=~/.nvim/bundle/vundle
call vundle#begin('~/.nvim/bundle/')
Plugin 'gmarik/vundle'
Plugin 'chriskempson/base16-vim'
call vundle#end()

filetype plugin indent on
syntax on
scriptencoding utf-8
set background=light
let base16colorspace=256 
colorscheme base16-google

set autoindent
set autoread
set backspace=indent,eol,start whichwrap=b,s,h,l,<,>,[,] nowrap
set clipboard=unnamed
set encoding=utf-8
set nojoinspaces
set hidden
set history=1000
set ignorecase incsearch hlsearch smartcase
set laststatus=2 ruler showcmd
set scrolloff=1
set shortmess+=IfilmnrxoOtT
set expandtab shiftwidth=4 softtabstop=4 tabstop=8
set mouse=a mousehide
set noerrorbells visualbell t_vb=
set winminheight=0
set backup
set completeopt=menu,longest
set wildmenu
set splitright
if has('persistent_undo')
    set undofile undolevels=1000 undoreload=10000
endif

let mapleader=' '
" Easier horizontal shifting.
map zl zL
map zh zH
" Wrapped lines goes down/up to next row, rather than next line in file.
nnoremap j gj
nnoremap k gk
" Yank from the cursor to the end of the line, to be consistent with C and D.
nnoremap Y y$
" Shift without exiting visual mode.
vnoremap < <gv
vnoremap > >gv
" Stop highlighting after a search.
map <silent> <leader>/ :nohlsearch<cr>
" Toggle paste mode.
map <silent> <leader>p :set paste!<cr>
" Source vim configuration.
map <silent> <leader>v :source $HOME/.vimrc<cr>
nnoremap <c-h> <c-w>h
nnoremap <c-j> <c-w>j
nnoremap <c-k> <c-w>k
nnoremap <c-l> <c-w>l
tnoremap <c-h> <c-\><c-n><c-w>h
tnoremap <c-j> <c-\><c-n><c-w>j
tnoremap <c-k> <c-\><c-n><c-w>k
tnoremap <c-l> <c-\><c-n><c-w>l
tnoremap <c-w> <c-\><c-n><c-w>

" Only strip trailing whitespace for certain file types.
autocmd FileType c,cpp,java,go,php,javascript,python,twig,xml,yml,perl,lua
            \ autocmd BufWritePre <buffer> call StripTrailingWhitespace()
" Modula2, really?
autocmd BufRead,BufNewFile *.md setlocal filetype=markdown spell textwidth=80
" Automatically switch to the current file directory when a new buffer is
" opened.
autocmd BufEnter * if bufname("") !~ "^\[A-Za-z0-9\]*://" | lcd %:p:h | endif
" Highlight characters beyond 80 columns.
autocmd BufWinEnter,WinEnter * let w:m2=matchadd('ColorColumn', '\%>80v.\+', -1)
" Go style.
autocmd FileType go setlocal ts=8 sw=8 sts=8 tw=80 noet
" Unix style.
autocmd FileType c,cpp,sh,make,zsh,gitconfig setlocal ts=8 sw=8 sts=8 tw=80 noet cin fo=tcqlron cino=:0,l1,t0,g0,(s,m1,j1,J1

" Setup directories for backup, view and swap files under ~/.vim/.
function! InitializeDirectories()
    let parent = $HOME
    let prefix = 'nvim/'
    let dir_list = {
                \ 'backup': 'backupdir',
                \ 'views': 'viewdir',
                \ 'swap': 'directory',
                \ }
    if has('persistent_undo')
        let dir_list['undo'] = 'undodir'
    endif
    let common_dir = parent . '/.' . prefix
    for [dirname, settingname] in items(dir_list)
        let directory = common_dir . dirname . '/'
        if exists("*mkdir")
            if !isdirectory(directory)
                call mkdir(directory)
            endif
        endif
        if !isdirectory(directory)
            echo "Warning: Unable to create backup directory: " . directory
            echo "Try: mkdir -p " . directory
        else
            let directory = substitute(directory, " ", "\\\\ ", "g")
            exec "set " . settingname . "=" . directory
        endif
    endfor
endfunction
call InitializeDirectories()

function! StripTrailingWhitespace()
    " Preparation: save last search, and cursor position.
    let _s=@/
    let l = line(".")
    let c = col(".")
    " Let's do this!
    %s/\s\+$//e
    " Cleanup: restore previous search history, and cursor position
    let @/=_s
    call cursor(l, c)
endfunction
