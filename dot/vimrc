set nocompatible
filetype off
set rtp+=~/.vim/bundle/vundle
call vundle#begin()
Plugin 'gmarik/vundle'
Plugin 'tpope/vim-surround'
Plugin 'tpope/vim-repeat'
Plugin 'tpope/vim-commentary'
Plugin 'tpope/vim-unimpaired'
Plugin 'tpope/vim-abolish'
Plugin 'tpope/vim-vinegar'
Plugin 'tpope/vim-endwise'
Plugin 'tpope/vim-fugitive'
Plugin 'kana/vim-textobj-user'
Plugin 'kana/vim-textobj-indent'
Plugin 'altercation/vim-colors-solarized'
Plugin 'Lokaltog/vim-easymotion'
Plugin 'christoomey/vim-tmux-navigator'
Plugin 'scrooloose/syntastic'
Plugin 'hynek/vim-python-pep8-indent'
Plugin 'fatih/vim-go'
call vundle#end()
filetype plugin indent on

syntax off
scriptencoding utf-8
color solarized

runtime macros/matchit.vim
runtime ftplugin/man.vim

set autoindent
set autoread
set backspace=indent,eol,start whichwrap=b,s,h,l,<,>,[,] nowrap
set clipboard=unnamed
set laststatus=2 ruler showcmd
set encoding=utf-8
set nojoinspaces
set ignorecase incsearch hlsearch smartcase
set hidden
set history=1000
set scrolloff=1
set shortmess+=filmnrxoOtT
set expandtab shiftwidth=4 softtabstop=4 tabstop=8
set mouse=a mousehide
set noerrorbells visualbell t_vb=
set winminheight=0
set backup
set completeopt=menu,longest
set wildmenu
if has('persistent_undo')
    set undofile undolevels=1000 undoreload=10000
endif

set guifont=Monaco:h15
set guioptions-=rL

" Easier horizontal shifting.
map zl zL
map zh zH
" Wrapped lines goes down/up to next row, rather than next line in file.
nnoremap j gj
nnoremap k gk
" Use Vim for displaying man pages.
nnoremap <silent> K :<c-u>exe "Man" v:count "<c-r><c-w>"<cr>
" Yank from the cursor to the end of the line, to be consistent with C and D.
nnoremap Y y$
" Shift without exiting visual mode.
vnoremap < <gv
vnoremap > >gv
" Stop highlighting after a search.
map <silent> <leader>/ :nohlsearch<cr>
" Toggle paste mode.
map <silent> <leader>p :set paste!<cr>
" Source vim configuration.
map <silent> <leader>v :source $HOME/.vimrc<cr>
" Fast saving
nmap <leader>w :w!<cr>

command! W w !sudo tee % > /dev/null

" Only strip trailing whitespace for certain file types.
autocmd FileType c,cpp,java,go,php,javascript,python,twig,xml,yml,perl,lua
            \ autocmd BufWritePre <buffer> call StripTrailingWhitespace()
" Modula2, really?
autocmd BufRead,BufNewFile *.md setlocal filetype=markdown spell
" Automatically switch to the current file directory when a new buffer is
" opened.
autocmd BufEnter * if bufname("") !~ "^\[A-Za-z0-9\]*://" | lcd %:p:h | endif
" Highlight characters beyond 80 columns.
autocmd BufWinEnter,WinEnter * let w:m2=matchadd('ColorColumn', '\%>80v.\+', -1)
" Go style.
autocmd FileType go setlocal ts=8 sw=8 sts=8 tw=80 noet
" Unix style.
autocmd FileType c,cpp,sh,make,zsh setlocal ts=8 sw=8 sts=8 tw=80 noet cin fo=tcqlron cino=:0,l1,t0,g0,(s,m1,j1,J1

" EasyMotion
" Disable default mappings.
let g:EasyMotion_do_mapping = 0
" Turn on the case sensitive feature.
let g:EasyMotion_smartcase = 1
" Bi-directional find motion, jump to anywhere you want with minimal
" keystrokes, with just one key binding: `<space>{char}{label}`.
nmap <space> <plug>(easymotion-s)

" Syntastic
let g:syntastic_cpp_compiler_options='-std=c++11'

" Setup directories for backup, view and swap files under ~/.vim/.
function! InitializeDirectories()
    let parent = $HOME
    let prefix = 'vim/'
    let dir_list = {
                \ 'backup': 'backupdir',
                \ 'views': 'viewdir',
                \ 'swap': 'directory',
                \ }
    if has('persistent_undo')
        let dir_list['undo'] = 'undodir'
    endif
    let common_dir = parent . '/.' . prefix
    for [dirname, settingname] in items(dir_list)
        let directory = common_dir . dirname . '/'
        if exists("*mkdir")
            if !isdirectory(directory)
                call mkdir(directory)
            endif
        endif
        if !isdirectory(directory)
            echo "Warning: Unable to create backup directory: " . directory
            echo "Try: mkdir -p " . directory
        else
            let directory = substitute(directory, " ", "\\\\ ", "g")
            exec "set " . settingname . "=" . directory
        endif
    endfor
endfunction
call InitializeDirectories()

function! StripTrailingWhitespace()
    " Preparation: save last search, and cursor position.
    let _s=@/
    let l = line(".")
    let c = col(".")
    " Let's do this!
    %s/\s\+$//e
    " Cleanup: restore previous search history, and cursor position
    let @/=_s
    call cursor(l, c)
endfunction

function! s:RunShellCommand(cmdline)
    botright new
    setlocal buftype=nofile
    setlocal bufhidden=delete
    setlocal nobuflisted
    setlocal noswapfile
    setlocal nowrap
    setlocal filetype=shell
    setlocal syntax=shell
    call setline(1, a:cmdline)
    call setline(2, substitute(a:cmdline, '.', '=', 'g'))
    execute 'silent $read !' . escape(a:cmdline, '%#')
    setlocal nomodifiable
    1
endfunction
command! -complete=file -nargs=+ Shell call s:RunShellCommand(<q-args>)
